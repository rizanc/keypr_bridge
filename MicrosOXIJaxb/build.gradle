apply plugin: 'java'
apply plugin: 'idea'

project.ext.generatedSources = "$buildDir/generated-sources"

configurations {
    generate
}

sourceSets {
    main {
        java {
            srcDirs generatedSources
        }
    }
}

dependencies {
    compile project(':CloudKeyJaxbSupport')

    generate jaxb_xjc
    generate 'com.github.jaxb-xew-plugin:jaxb-xew-plugin:1.3'
    compile joda_time
}

task createGeneratedDir(dependsOn: processResources) << {
    mkdir generatedSources
}

task jaxb(dependsOn: createGeneratedDir) << {
    ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.generate.asPath)

    ant.xjc(destdir: generatedSources, binding: "$projectDir/src/main/bindings/binding.xjb") {
        arg(line: "-extension -Xxew -Xxew:summary $buildDir/xew-summary.txt -Xxew:instantiate lazy")

        schema(dir: "$projectDir/src/main/xsd", includes: "*.xsd")
        depends(dir: "$projectDir/src/main/bindings", includes: "*.xjb")
        depends(dir: "$projectDir/src/main/xsd", includes: "*.xsd")
        produces(dir: generatedSources, includes: "**/*.java")
    }
}


compileJava.dependsOn jaxb

idea {
    module {
        excludeDirs = []
        ["classes", "docs", "dependency-cache", "libs", "reports", "resources", "test-results", "tmp"].each {
            excludeDirs << file("$buildDir/$it")
        }
    }
}